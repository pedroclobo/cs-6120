# RUN: cat %s | bril2json | make-dom-tree -dom    | filecheck %s --check-prefix=DOM
# RUN: cat %s | bril2json | make-dom-tree -idom   | filecheck %s --check-prefix=IDOM
# RUN: cat %s | bril2json | make-dom-tree -dfront | filecheck %s --check-prefix=DFRONT

# DOM: entry: entry
# DOM-NEXT: loop: entry, loop
# DOM-NEXT: body: entry, loop, body
# DOM-NEXT: then: entry, loop, body, then
# DOM-NEXT: endif: entry, loop, body, endif
# DOM-NEXT: exit: entry, loop, exit

# IDOM: entry -> (NULL)
# IDOM-NEXT: loop -> entry
# IDOM-NEXT: body -> loop
# IDOM-NEXT: then -> body
# IDOM-NEXT: endif -> body
# IDOM-NEXT: exit -> loop

# DFRONT: entry: {}
# DFRONT-NEXT: loop: {loop}
# DFRONT-NEXT: body: {loop}
# DFRONT-NEXT: then: {endif}
# DFRONT-NEXT: endif: {loop}
# DFRONT-NEXT: exit: {}
@main {
.entry:
  x: int = const 0;
  i: int = const 0;
  one: int = const 1;

.loop:
  max: int = const 10;
  cond: bool = lt i max;
  br cond .body .exit;

.body:
  mid: int = const 5;
  cond: bool = lt i mid;
  br cond .then .endif;

.then:
  x: int = add x one;
  jmp .endif;

.endif:
  factor: int = const 2;
  x: int = mul x factor;

  i: int = add i one;
  jmp .loop;

.exit:
  print x;
}
